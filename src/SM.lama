-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> x
    esac
  }

  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SWAP             -> "SWAP"
  | SEXP   (n)       -> sprintf ("SEXP %d", n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

fun arrayify (x) {
  case x of
    Sexp (tag, args) -> listArray(tag : map(arrayify, args))
  | _ -> x
  esac
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  local map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  local globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, val) {
    case loc of
      Arg (i) -> args[i] := val
    | Loc (i) -> locs[i] := val
    | Glb (x) -> local g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then val else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  (* Assignment *)
  fun eval (c@[st, cst, s, w], insns) {
    case insns of
    {}     -> c
  | ins : insns_ ->
     -- printf("%s (stack depth %d)\n", showSMInsn(ins), size(st));

      eval(case ins of
        CONST (x)  -> [ x : st, cst, s, w ]
      | STRING (x) -> [ x : st, cst, s, w ]
      | ARRAY (n)  -> case take (st, n) of [st, arr] -> [listArray (arr) : st, cst, s, w] esac
      | BINOP (op) -> case st of b : a : rest -> [ evalOp (op, a, b) : rest, cst, s, w ] esac
      | LD    (x)  -> [ lookup(s, x) : st, cst, s, w ]
      | LDA   (x)  -> [ Ref (x) : st, cst, s, w ]
      | ST    (x)  -> case st of a : rest -> assign(s, x, a); [ rest, cst, s, w ] esac
      | STI        -> case st of a : Ref (x) : rest -> assign (s, x, a); [ a : rest, cst, s, w ] esac
      | SWAP       -> case st of a : b : rest -> [ b : a : rest, cst, s, w ] esac
      | STA        -> case st of a : idx : arr : rest -> arr[idx] := a; [ a : rest, cst, s, w ] esac
      | ELEM       -> case st of idx : arr : rest -> [ arrayify(arr)[idx] : rest, cst, s, w ] esac
      | DUP        -> case st of a : rest -> [ a : a : rest, cst, s, w ] esac
      | DROP       -> case st of a : rest -> [ rest, cst, s, w ] esac
      | CJMP (cond, where) -> case st of a : rest -> [ rest, cst, s, w ] esac
      | CALL (func, argc) -> [st, [s, insns_] : cst, s, w]
      | SEXP (n) ->
        case take (st, n) of [st, args] ->
          case st of tag : rest ->
            [ Sexp (tag, args) : rest, cst, s, w ]
          esac
        esac
      | BUILTIN (name, argc) ->
        case take (st, argc) of [st, argv] ->
          case evalBuiltin (name, argv, w) of [retval, w] ->
            [ retval : st, cst, s, w ]
          esac
        esac
      | GLOBAL (x) -> assign (s, Glb (x), 0); [st, cst, s, w]
      | BEGIN (func, argc, varc) ->
        case take (st, argc) of [st, argv] ->
          [st, cst, foldl (fun ([s, i], arg) {
            assign (s, Arg (i), arg);
            [s, i + 1]
          }, [makeState (argc, varc), 0], argv)[0], w]
        esac
      | END -> case cst of [s, _] : cst -> [st, cst, s, w] | _ -> c esac
      | _          -> c
      esac, case ins of
          JMP (where) -> fromLabel (env, where)
        | CALL (func, argc) -> fromLabel (env, func)
        | CJMP (cond, where) -> case st of a : rest ->
          if (a == (case cond of "z" -> 0 | _ -> 1 esac)) then fromLabel (env, where) else insns_ fi esac
        | END -> case cst of [_, ret] : cst -> ret | _ -> {} esac
        | _ -> insns_
      esac)
  esac
  }
  (* End *)

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a local variable for the current function
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups a name of a variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Fun (_, _) -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    | x          -> x
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case name of
      "read" -> Builtin
    | "write" -> Builtin
    | _ ->
      case lookup (state, name) of
        x@Fun (_, _) -> x
      | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
      esac
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   beginFun,
   getFuns,
   lookupVar,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun beginFun (env, state) {
  env [6] (state)
}

fun getFuns (env) {
  env [7] ()
}

fun lookupVar (env, name) {
  env [8] (name)
}

fun lookupFun (env, name) {
  env [9] (name)
}

fun getLocals (env) {
  env [10] ()
}

fun genFunLabel (env, name) {
  env [11] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [12] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [13] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then singleton (env)
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}
-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

(* Assignment *)

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).


fun compileSMMatch (env, pattern) {
  case pattern of
    Var ("_") -> [env, fun (x) {x}, {CONST (1)}]
  | Var (x) ->
      case addVar(beginScope(env), x) of env ->
        [env, fun (x) { endScope(x) }, {
          DUP,
          LDA(lookupVar(env, x)),
          SWAP,
          STI,
          DROP,
          CONST (1)
        }]
      esac
  | Sexp (tag, args) ->
      case genLabels (env, 2) of [lEnd, lFail, env] ->
        case foldl(fun ([env, cont, res, i], arg) {
          case compileSMMatch(env, arg) of [env2, cont2, code] ->
            [env2, fun (x) { cont(cont2(x)) },
            {
              res,
              DUP,
              CONST (i+1),
              ELEM,
              code,
              SWAP,
              DROP,
              CJMP ("z", lFail)
            }, i+1]
          esac
        }, [env, fun (x) {x}, {
          DUP,
          CONST (0),
          ELEM,
          STRING (tag),
          BUILTIN ("strcmp", 2),
          CJMP ("z", lFail),
          DUP,
          BUILTIN ("length", 1),
          CONST (size(args) + 1),
          BINOP ("=="),
          CJMP ("z", lFail)
        }, 0], args) of [env, cont, res, _] ->
          [env, cont, {
            res,
            CONST (1),
            JMP (lEnd),
            LABEL (lFail),
            CONST (0),
            LABEL (lEnd)
          }]
        esac
      esac
  esac
}

-- Compiles an expression into a stack machine code.
fun compileSMEnv (env, stmt) {
  case stmt of
      Binop (op, lhs, rhs) ->
      case compileSMEnv (env, lhs) of [env, lhs] ->
        case compileSMEnv (env, rhs) of [env, rhs] ->
          [env, { lhs, rhs, BINOP(op) }]
        esac
      esac
    | Assn (lhs, a) ->
      case compileSMEnv (env, a) of [env, rhs] ->
        case lhs of
        ElemRef (arr, idx) ->
          case compileSMEnv (env, arr) of [env, arr] ->
            case compileSMEnv (env, idx) of [env, idx] ->
              [env, { arr, idx, rhs, STA }]
            esac
          esac
        | _ ->
          case compileSMEnv (env, lhs) of [env, lhs] ->
            [env, { lhs, rhs, STI }]
          esac
        esac
      esac
    | Seq (a, b) ->
      case compileSMEnv (env, a) of [env, sm1] ->
        case compileSMEnv (env, b) of [env, sm2] ->
          [env, { sm1, sm2 }]
        esac
      esac
    | Skip -> [env, {}]
    | If (cond, body1, body2) ->
      case genLabels(env, 2) of [lElse, lEnd, env] ->
        case compileSMEnv (env, body1) of [env, body1] ->
          case compileSMEnv (env, body2) of [env, body2] ->
            case compileSMEnv (env, cond) of [env, cond] ->
              [env, {
                cond,
                CJMP ("z", lElse),
                body1,
                JMP (lEnd),
                LABEL (lElse),
                body2,
                LABEL (lEnd)
              }]
            esac
          esac
        esac
      esac
    | While (cond, body) ->
      case genLabels(env, 2) of [lBegin, lEnd, env] ->
        case compileSMEnv (env, body) of [env, body] ->
          case compileSMEnv (env, cond) of [env, cond] ->
            [env, {
              LABEL (lBegin),
              cond,
              CJMP ("z", lEnd),
              body,
              JMP (lBegin),
              LABEL (lEnd)
            }]
          esac
        esac
      esac
    | Repeat (body, cond) ->
      case genLabels(env, 1) of [lBegin, env] ->
        case compileSMEnv (env, body) of [env, body] ->
          case compileSMEnv (env, cond) of [env, cond] ->
            [env, {
              LABEL (lBegin),
              body,
              cond,
              CJMP ("z", lBegin)
            }]
          esac
        esac
      esac
    | Var (x) -> [env, LD (lookupVar(env, x)) : {}]
    | Ref (x) -> [env, LDA (lookupVar(env, x)) : {}]
    | Const (x) -> [env, CONST (x) : {}]
    | String (x) -> [env, STRING (x) : {}]
    | Array (xs) ->
      case foldl (fun ([env, xs], x) {
        case compileSMEnv (env, x) of [env, t] ->
          [env, {xs, t}]
        esac
      }, [env, {}], xs) of [env, ts] ->
        [env, {ts, ARRAY (size(xs))}]
      esac
    | Elem (arr, idx) ->
      case compileSMEnv (env, arr) of [env, arr] ->
        case compileSMEnv (env, idx) of [env, idx] ->
          [env, {arr, idx, ELEM}]
        esac
      esac
    | Ignore (x) ->
      case compileSMEnv (env, x) of [env, t] ->
        [env, {t, DROP}]
      esac
    | Scope (defs, x) ->
      case foldl (fun ([env, globals], def) {
        case def of
          Local (names) -> [addVars (env, names),
            if isGlobal (env) then
              map(fun (x) { GLOBAL (x) }, names) +++ globals
            else globals fi
          ] |
          Fun (name, params, body) ->
            case genFunLabel (env, name) of [label, env] ->
              [addFun (env, name, label, params.size), globals]
            esac
        esac
      }, [beginScope(env), {}], defs) of [env, globals] ->
        case foldl (fun (env, def) {
          case def of Fun (name, params, body) ->
            rememberFun (env, lookupFun (env, name)[0], params, body)
            | _ -> env
          esac
        }, env, defs) of env ->
          case compileSMEnv (env, x) of [env, t] ->
            [endScope (env), { globals, t }]
          esac
        esac
      esac
    | Call (name, args) ->
        case foldl (fun ([env, res], arg) {
          case compileSMEnv (env, arg) of [env, t] ->
            [env, {res, t}]
          esac
        }, [env, {}], args) of [env, res] ->
        case lookupFun (env, name) of Fun (label, n) ->
          [env, {res, CALL (label, n)}]
        | Builtin -> [env, {res, BUILTIN (name, size(args))}]
        esac
      esac
    | Builtin (name, args) ->
      case foldl (fun ([env, res], arg) {
        case compileSMEnv (env, arg) of [env, t] ->
          [env, {res, t}]
        esac
      }, [env, {}], args) of [env, res] ->
        [env, {res, BUILTIN (name, size(args))}]
      esac
    | Sexp (tag, args) ->
      case foldl (fun ([env, res], arg) {
        case compileSMEnv (env, arg) of [env, t] ->
          [env, {res, t}]
        esac
      }, [env, {STRING (tag)}], args) of [env, res] ->
        [env, {res, SEXP (size(args))}]
      esac
    | Case (x, branches) ->
      case compileSMEnv (env, x) of [env, x] ->
        case genLabels(env, 2) of [lEnd, lHack, env] ->
          case foldl (fun ([env, res], [pattern, body]) {
            case compileSMMatch (env, pattern) of [env, cont, match] ->
              case compileSMEnv (env, body) of [env, body] ->
                case genLabel(env) of [lSkip, env] ->
                  [cont (env), {
                    res,
                    match,
                    CJMP ("z", lSkip),
                    DROP,
                    body,
                    JMP (lEnd),
                    LABEL (lSkip)
                  }]
                esac
              esac
            esac
          }, [env, {}], branches) of [env, code] ->
            [env, {
              x,
              LABEL (lHack),
              code,
              JMP (lHack),
              LABEL (lEnd)
            }]
          esac
        esac
      esac
  esac
}

fun compileSMFuns (env, sm) {
  case getFuns (env) of [funs, env] ->
    case funs of {} -> [env, sm] | _ ->
      case foldl (fun ([env, sm], Fun (name, params, body, st)) {
        case addArgs(beginFun (env, st), params) of env ->
          case compileSMEnv(env, body) of [env, bodySM] ->
            [env, {
              sm,
              LABEL (name),
              BEGIN (name, params.size, getLocals (env)),
              bodySM,
              END
            }]
          esac
        esac
      }, [env, sm], funs) of [env, sm] ->
        compileSMFuns (env, sm)
      esac
    esac
  esac
}

public fun compileSM (stmt) {
  case compileSMEnv (beginScope(initCompEnv()), stmt) of [env, sm] ->
    deepFlatten(compileSMFuns (env, {
      LABEL ("main"),
      BEGIN ("main", 0, getLocals (env)),
      sm,
      END
    })[1])
  esac
}
(* End *)
