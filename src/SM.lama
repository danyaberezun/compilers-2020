-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c@[s, st, w@[i,o]], insns) {
 case insns of
    {} -> c
   | READ      : e -> local var_ = readWorld(w);
                      eval([var_[0] : s, st, var_[1]], e)
   | WRITE     : e -> eval([s[1], st, writeWorld(s[0], w)], e)
   | CONST(n)  : e -> eval([n : s, st, w], e)
   | BINOP(op) : e -> eval([evalOp(op, s[1][0], s[0]) : s[1][1], st, w], e)
   | LD(x)     : e -> eval([st(x) : s, st, w], e)
   | ST(x)     : e -> eval([s[1], st <-[x, s[0]], w], e)
   esac
}
-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list (of, possibly, lists)
-- of stack machine instructions

fun compileExpr (expr) {
case expr of
    Var(x) -> singleton(LD(x))
   | Const(n) -> singleton(CONST(n))
   | Binop(op, x, y) -> compileExpr(x) +++ compileExpr(y) +++ singleton(BINOP(op))
esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
case stmt of
     Assn(x, expr) -> compileExpr(expr) +++ singleton(ST(x))
   | Read(x) -> singleton(READ) +++ singleton(ST(x))
   | Write(expr) -> compileExpr(expr) +++ singleton(WRITE)
   | Seq(s1, s2) -> compileSM(s1) +++ compileSM(s2)
   | Skip -> {}
esac
}
