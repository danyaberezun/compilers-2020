-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
  	case c of
		[s, st, w@[i,o]] -> case insns of
		{} -> c
		|READ: p -> case i of x : it -> eval([x : s, st, [it, o]], p) esac
		|WRITE: p -> case s of x : ss -> eval([ss, st, [i, x : o]], p) esac
		|BINOP(b): p -> case s of x : y: ss -> eval([binopOp(b, y, x): ss, st, w], p) esac
		|CONST(n): p -> eval([n : s, st, w], p)
		|LD(x): p -> eval([st(x) : s, st, w], p)
		|ST(x): p -> case s of y : ss -> eval([s, (st <- [x,y]), w], p) esac
     esac
    esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list (of, possibly, lists)
-- of stack machine instructions
fun compileExpr (expr) {
	case expr of
		Var(x) -> singleton(LD(x))
		|Const(n) -> singleton(CONST(n))
		|Binop (op, lim1, lim2) -> compileExpr(lim1) +++ compileExpr(lim2) +++ singleton(BINOP(op))
	esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
	case stmt of
		Read(x) -> {READ, ST(x)}
		|Write(x) -> compileExpr(x) +++ singleton(WRITE)
     	|Assn(x, e) -> compileExpr(e) +++ singleton(ST(x))
    	|Seq(st1,st2) ->compileSM(st1) +++ compileSM(st2)
    	|Skip -> {}
	esac
}
