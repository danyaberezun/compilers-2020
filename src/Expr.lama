-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (expr, expr)         |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Var (str) -> [c, s (str)]
  | Const (val) -> [c, val]
  | Binop (str, e1, e2) -> 
      case evalList (c, {e1, e2}) of
        [c_, {l, r}] -> [c_, evalOp (str, l, r)]
      esac
  | Assn (e1, e2) -> 
      case evalList (c, {e1, e2}) of
        [[s_, w_], {Ref (x), v}] -> [[s_ <- [x, v], w_], v]
      esac
  | Seq (e1, e2) -> eval (eval (c, e1).fst, e2)
  | Skip -> [c, Void]
  | Read (str) -> 
      case readWorld (w) of
        [v, w_] -> [[s <- [str, v], w_], Void]
      esac
  | Write (e) -> 
      case eval (c, e) of
        [[s_, w_], v] -> [[s_, writeWorld (v, w_)], Void]
      esac
  | If (cond, e1, e2) -> 
      case eval (c, cond) of
        [c_, 0] -> eval (c_, e2)
      | [c_, _] -> eval (c_, e1)
      esac
  | While (cond, e) ->
      case eval (c, cond) of
        [c_, 0] -> [c_, Void]
      | [c_, _] -> eval (eval (c_, e).fst, While (cond, e))
      esac
  | Repeat (e, cond) -> 
      case eval (eval (c, e).fst, cond) of
        [c_, 0] -> eval (c_, Repeat (e, cond))
      | [c_, _] -> [c_, Void]
      esac
  | Ref (str) -> [c, Ref (str)]
  | Ignore (e) -> [eval (c, e).fst, Void]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
