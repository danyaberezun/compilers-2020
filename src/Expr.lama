-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (expr, expr)         |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Read (x)     -> case readWorld (w) of [val, w1] -> [[s <- [x, val], w1], Void] esac |
    Write (e)    -> case eval(c, e) of [[s, w], val] ->  [[s, writeWorld (val, w)], Void] esac |
    Assn (x, e)  -> case evalList(c, {e, x}) of [[s, w], val:ref:_] -> [[s <- [ref, val], w], val] esac |
    Skip -> [c, Void] |
    If (cond, s1, s2) -> case eval(c, cond) of 
        [c, val] -> if val then eval(c, s1) else eval(c, s2) fi
    esac | 
    wh@While (cond, st) -> case eval(c, cond) of 
        [c, val] -> if val then case eval(c, st) of [c, _] -> eval(c, wh) esac
                    else [c, Void] 
                    fi
    esac | 
    r@Repeat (st, cond) -> case evalList(c, {st, cond}) of 
        [c, _:val:_] -> if val then [c, Void] else eval(c, r) fi
    esac |
    Seq (s1, s2) -> case evalList(c, {s1, s2}) of [c, _:res:_] -> [c, res] esac |
    Var (x) -> [c, s(x)] |
    Ref (x) -> [c, x] |
    Const (val) -> [c, val] |
    Binop (op, lhs, rhs) -> case evalList(c, {lhs, rhs}) of 
        [c, lval:rval:_] -> [c, evalOp(op, lval, rval)]
    esac |
    Ignore (e) -> [eval(c, e).fst, Void]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
