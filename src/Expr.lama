-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (expr, expr)         |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
	case expr of
		  Assn   (var, e)       -> case evalList (c, {var, e}) of [[s_n, w_n], {Ref(x), val}] -> [[s_n <- [x, val], w_n], val] esac
		| Seq    (e1, e2)       -> case eval (c, e1) of [c_n, n] -> eval (c_n, e2) esac
		| Skip                  -> [c, None]
		| Read   (var)          -> case readWorld (w) of [val, w_n] -> [[s <- [var, val], w_n], None] esac
		| Write  (e)            -> case eval (c, e) of [[s_n, w_n], val] -> [[s_n, writeWorld(val, w_n)], None] esac
		| If     (cond, e1, e2) -> case eval (c, cond) of [c_n, res] -> if (res) then eval (c_n, e1) else eval (c_n, e2) fi esac
		| While  (cond, e)      -> case eval (c, cond) of [c_n, res] -> if (res) then 
																			case eval (c_n, e) of [c_nn, n] -> eval (c_nn, While(cond, e)) esac 
																			else [c_n, None]
																		fi esac
		| Repeat (e, cond)      -> case eval (c, e) of [c_n, n] -> case eval (c_n, cond) of [c_nn, res] -> if (res) then
																												[c_nn, None]
																												else eval (c_nn, Repeat(e, cond))
																										   fi esac esac
		| Var    (var)          -> [c, s(var)]
		| Ref    (var)          -> [c, Ref(var)]
		| Const  (n)            -> [c, n]
		| Binop  (op, e1, e2)   -> case evalList (c, {e1, e2}) of [c_n, {v1, v2}] -> [c_n, evalOp(op, v1, v2)] esac
		| Ignore (e)            -> case eval (c, e) of [c_n, n] -> [c_n, None] esac
		| _                     -> failure ("No such expression as %s.\n", expr.string)
	esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
