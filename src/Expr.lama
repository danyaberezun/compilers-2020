-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (expr, expr)         |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, w], expr) {
  --printf("Evaluating %s\n", expr.string);
  case expr of
	    Const (n) -> [c, n]
	  | Var (x) -> [c, st(x)]
    | Binop (op, l, r) -> case evalList (c, {l, r}) of
				[new_c, {value1, value2}] -> [new_c, evalOp (op, value1, value2)] 
			        esac
    | Assn (to, from) -> case evalList (c, {to, from}) of
				[new_c@[new_st, new_w], {Ref (place), value}] -> [[new_st <- [place, value], new_w], value]
			     esac
    | Skip -> [c, None]
    | Seq (expr1, expr2) -> case eval (c, expr1) of
					[new_c, value] -> eval (new_c, expr2)
				  esac
    | Read (to) -> case readWorld (w) of
				[value, new_w] -> [[st <- [to, value], new_w], None]
		       esac
    | Write(expression) -> case eval(c,expression) of
        [new_c@[new_st, new_w], value] -> [[new_st, writeWorld (value, new_w)], None]
            esac
    | If (cond, expr1, expr2) -> case eval (c, cond) of
						  [new_c, 0] -> eval (new_c, expr2)
						| [new_c, _ ] -> eval (new_c, expr1)
					  esac
    | While (cond, expression) -> case eval (c, cond) of
						[new_c, 0] -> [new_c, None]
						| [new_c, _ ] -> case eval (new_c, expression)  of
									          [latest_c, value] -> eval (latest_c, expr)
								         esac
					            esac
    | Repeat (expression, cond) -> eval (c, Seq (expression, While (Binop ("==", cond, Const (0)), expression))) 
	  | Ref (place) -> [c, Ref (place)]
	  | Ignore (expression) -> case eval (c, expression) of
					[new_c, value] -> [new_c, None]
				 esac 
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
