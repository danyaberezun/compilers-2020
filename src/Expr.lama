
-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (expr, expr)         |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Ignore (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[state, world], expr) {
case expr of
	Const (n) -> [c, n]
	| Var (x) -> [c, state (x)]
        | Binop (op, l, r) -> case evalList (c, {l, r}) of
				[n_c, {value1, value2}] -> [n_c, evalOp (op, value1, value2)] 
			      esac
	| Assn (to, from) -> case evalList (c, {to, from}) of
				[n_c@[n_state, n_world], {Ref (place), value}] -> [[n_state <- [place, value], n_world], value]
			     esac
	| Seq (expr1, expr2) -> case eval (c, expr1) of
					[n_c, value] -> eval (n_c, expr2)
				esac
	| Skip -> [c, None]
	| Read (to) -> case readWorld (world) of
				[value, n_world] -> [[state <- [to, value], n_world], None]
		       esac
	| Write (expression) -> case eval (c, expression) of
					[n_c@[n_state, n_world], value] -> [[n_state, writeWorld (value, n_world)], None]
				esac
	| If (condition, expr1, expr2) -> case eval (c, condition) of
						[n_c, 0] -> eval (n_c, expr2)
						| [n_c, _ ] -> eval (n_c, expr1)
					  esac
	| While (condition, expr1) -> case eval (c, condition) of
						[n_c, 0] -> [n_c, None]
						| [n_c, _ ] -> case eval (n_c, expr1)  of
									[latest_c, value] -> eval (latest_c, expr)
								 esac
					   esac
	| Repeat (expr1, condition) -> eval (c, Seq (expr1, While (Binop ("==", condition, Const (0)), expr1))) 
	| Ref (x) -> [c, Ref (x)]
	| Ignore (expression) -> case eval (c, expression) of
					[n_c, value] -> [n_c, None]
				 esac 
esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
