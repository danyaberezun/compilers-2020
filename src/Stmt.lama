-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c@[state, world@[input, output]], stmt) {
  case stmt of 
    Assn (x, expr) -> [state <- [x, expression (state, expr)], world]
    | Seq (s1, s2) -> eval (eval(c, s1), s2 )
    | Read (x) -> [state <- [x, fst (readWorld (world))], snd (readWorld (world))]
    | Write (expr) -> [state, writeWorld (expression (state, expr), world)]
    | Skip -> c
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
