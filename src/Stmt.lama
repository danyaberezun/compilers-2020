-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
fun eval (c, stmt) {
  case c of [s, w] ->
    case stmt of
      Read (x)     -> case readWorld (w) of [h, w1] -> [s <- [x, h], w1] esac
    | Write (e)    -> [s, writeWorld (evalExpr (s, e), w)]
    | Assn (x, e)  -> [s <- [x, evalExpr (s, e)], w]
    | Seq (s1, s2) -> eval (eval (c, s1), s2)
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
