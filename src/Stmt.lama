-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
fun eval (c, stmt) {
  case stmt of
    Assn (x, e)  -> [(c[0] <- [x, evalExpr (c[0], e)]), c[1] ]
  | Read (x)     -> 
      local r; 
      r := readWorld (c[1]); 
      [c[0] <- [x, r[0]], r[1]]
  | Write (e)    -> [c[0], writeWorld (evalExpr (c[0], e), c[1])]
  | Seq (s1, s2) -> eval (eval (c, s1), s2)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
