-- Parser

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- flip Binop
fun binop(lhs, op, rhs) {
  Binop (op, lhs, rhs)
}

local opTable = {
  [Left, [s ("!!"), binop] : {}],
  [Left, [s ("&&"), binop] : {}],
  [Nona, [s ("==") | s ("!=") | s ("<") | s ("<=") | s (">") | s (">="), binop] : {}],
  [Left, [s ("+") | s ("-"), binop] : {}],
  [Left, [s ("*") | s ("/") | s ("%"), binop] : {}]
};

-- Primary expression
local primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                   x=lident  {Var (x)}               |
                                   inbr[s("("), exp, s(")")]),
      exp = memo $ eta expr (opTable, primary) (id);

local stmt = memo $ eta syntax (
  x=kSkip {Skip} |
  x=lident s[":="] y=exp {Assn (x, y)} |
  x1=stmt s[";"] x2=stmt end {Seq (x1, x2)} |
  kRead x=inbr[s("("), lident, s(")")] {Read (x)} |
  kWrite x=inbr[s("("), exp, s(")")] {Write (x)}
);


-- Public top-level parser
public parse = stmt;

