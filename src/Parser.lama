-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop(op) {
    fun f(l, o, r) {
        Binop(o, l, r)
    }
    [s(op), f]
}


-- Primary expression
local primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                   x=lident  {Var (x)}               |
                                   inbr[s("("), exp, s(")")]),
      exp = memo $ eta expr({
          [Left, binop("!!") : {}],
          [Left, binop("&&") : {}],
          [Nona, map(binop, {"==", "<=", ">=", "<", ">", "!="})],
          [Left, map(binop, {"+", "-"})],
          [Left, map(binop, {"*", "/", "%"})]
      }, primary)(id);

local elseBranch = memo $ eta syntax(
    kFi {Skip}|
    -kElse stmt -kFi|
    kElif cond=exp kThen s1=stmt s2=elseBranch {If (cond, s1, s2)}
),primaryStmt = memo $ eta syntax (
    kWrite x=inbr[s("("), exp, s(")")] {Write(x)} |
    kRead x=inbr[s("("), lident, s(")")] {Read(x)} |
    kSkip {Skip} |
    x=lident s[":="] e=exp {Assn(x, e)} |
    kIf cond=exp kThen s1=stmt s2=elseBranch {If (cond, s1, s2)} |
    kWhile cond=exp kDo s=stmt kOd {While (cond, s)} |
    kRepeat s=stmt kUntil cond=exp {Repeat (s, cond)} |
    kFor init=stmt s[","] cond=exp s[","] step=stmt kDo s=stmt kOd {Seq (init, While (cond, Seq (s, step)))}
),stmt= memo $ eta syntax (primaryStmt|
s1=primaryStmt s[";"] s2=stmt {Seq(s1, s2)});

-- Public top-level parser
public parse = stmt;
             
