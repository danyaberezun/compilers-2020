-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, val, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (val)
  | _    -> val
  esac
}

fun assertVoid (atr, val, loc) {
  case atr of
    Void -> val
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}


fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

local elifParser = memo $ eta syntax (
									kElse st = exp kFi                                {st}                                                           |
									kFi                                               {fun (a) {assertVoid(a, Skip, loc)}}                           |
									kElif x = exp kThen s1 = exp s2 = elifParser      {fun (a) {If (x(Val), s1(a), s2(a))}}
									);

local primary    = memo $ eta syntax (
                                    -- decimal constant
                                    loc = pos x = decimal                                   {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}  |

                                    -- identifier
                                    x = lident                                              {fun (a) {
                                                                                             case a of
                                                                                               Ref  -> Ref (x)
                                                                                             | Void -> Ignore (Var (x))
                                                                                             | _    -> Var (x)
                                                                                             esac     
																							}}                                                       |
									inbr[s ("("), exp, s (")")]                                                                                      |
									loc = pos kWrite v = inbr[s("("), exp, s(")")]          {fun (a) {assertVoid (a, Write(v(Val)), loc)}}           |
									loc = pos kRead  v = inbr[s("("), lident, s(")")]       {fun (a) {assertVoid (a, Read(v), loc)}}                 |
									loc = pos kSkip                                         {fun (a) {assertVoid (a, Skip, loc)}}                    |
									loc = pos kWhile x = exp kDo st = exp kOd               {fun (a) {assertVoid (a, While(x(Val), st(Void)), loc)}} |
									loc = pos kIf x = exp kThen s1 = exp s2 = elifParser    {fun (a) {If (x(Val), s1(a), s2(a))}}                    |
									loc = pos kRepeat st = exp 
												kUntil x = inbr[s("("), exp, s(")")]        {fun (a) {assertVoid (a, Repeat(st(Void), x(Val)), loc)}}|
									loc = pos kFor s1 = exp s[","] e = exp s[","] s2 = exp kDo
												s3 = exp kOd  {fun (a) {assertVoid (a, Seq(s1(Void), While(e(Val), Seq(s3(Void), s2(Void)))), loc)}}),

      basic      = memo $ eta (expr ({[Right, singleton ([s (":="),
                                                        fun (l, loc, r) {
                                                          fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                                        }])],
                                    [Left , map (binop, singleton ("!!"))],
                                    [Left , map (binop, singleton ("&&"))],
                                    [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                    [Left , map (binop, {"+", "-"})],
                                    [Left , map (binop, {"*", "/", "%"})]
                                   },
                                   primary)),
      exp        = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
