-- Parser

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun operator_descriptor (str) {
  [s (str), fun (l, op, r) {Binop (op, l, r)}]
}
local operators = {
  [Left, singleton (
    operator_descriptor ("!!")
  )],
  [Left, singleton (
    operator_descriptor ("&&")
  )],
  [Nona, {
    operator_descriptor ("=="),
    operator_descriptor ("!="),
    operator_descriptor ("<="),
    operator_descriptor ("<"),
    operator_descriptor (">="),
    operator_descriptor (">")
  }],
  [Left, {
    operator_descriptor ("+"),
    operator_descriptor ("-")
  }],
  [Left, {
    operator_descriptor ("*"),
    operator_descriptor ("/"),
    operator_descriptor ("%")
  }]
};

-- Primary expression
local primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                   x=lident  {Var (x)}               |
                                   inbr[s("("), exp, s(")")]),
      exp = memo $ eta (expr (operators, primary));

local stmtElse = memo $ eta syntax (
    kElif e=exp kThen s1=stmt s2=stmtElse {If (e, s1, s2)}
  | kElse s=stmt kFi {s}
  | kFi {Skip}
  );
local stmt1 = memo $ eta syntax (
    kSkip {Skip}
  | kRead s["("] x=lident s[")"] {Read (x)}
  | kWrite s["("] e=exp s[")"] {Write (e)}
  | x=lident s[":="] e=exp {Assn (x, e)}
  | kIf e=exp kThen s1=stmt s2=stmtElse {If (e, s1, s2)}
  | kWhile e=exp kDo s=stmt kOd {While (e, s)}
  | kRepeat s=stmt kUntil e=exp {Repeat (s, e)}
  | kFor s1=stmt s[","] e=exp s[","] s2=stmt kDo s3=stmt kOd {Seq (s1, While (e, Seq (s3, s2)))}
  );

local semicolon_operator = singleton (
  [Left, singleton (
    [token (";"), fun (l, op, r) {Seq (l, r)}]
  )]
);
local stmt = memo $ eta (expr (semicolon_operator, stmt1));

-- Public top-level parser
public parse = stmt;
